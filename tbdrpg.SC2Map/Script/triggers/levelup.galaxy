bool gt_LevelUp_Func (bool testConds, bool runActions) {
  int lv_playerIndex = UnitGetOwner(EventUnit()) - 1;
  Player_r lr_player = gv_players[lv_playerIndex];
  TalentTree_r lr_talents = lib_talent_gv_playerTalents[lv_playerIndex];
  int lv_dragonEvolution;


  if (testConds) {
    if (lr_player.state.evolving || !gv_playersSetUp) {
      return false;
    }
  }

  if (!runActions) {
    return true;
  }

  lr_player.stats.level = FixedToInt(UnitGetPropertyFixed(lr_player.dragon, c_unitPropLevel, c_unitPropCurrent));

  lib_talent_gf_GrantTalentPoints(lr_talents, 1);
  ActorCreate(ActorScopeFromUnit(lr_player.dragon), "LevelUpEffect", null, null, null);
  SoundPlayOnUnit(SoundLink("LevelUp", -1), lr_player.playerGroupSingle, lr_player.dragon, 0.0, 100.0, 0.0);
  lr_player.stats.level = FixedToInt(UnitGetPropertyFixed(lr_player.dragon, c_unitPropLevel, c_unitPropCurrent));
  UIDisplayMessage(PlayerGroupAll(), c_messageAreaSubtitle, (PlayerName(lr_player.player) + (StringExternal("Param/Value/684E7509") + IntToText(lr_player.stats.level))));
  libNtve_gf_PlayAnimation(libNtve_gf_MainActorofUnit(lr_player.dragon), c_animNameDefault, "Dance", c_animFlagNonLooping, c_animTimeDefault);
  BoardItemSetText(gv_leadboard, 2, 2, FixedToText(UnitGetPropertyFixed(lr_player.dragon, c_unitPropLevel, c_unitPropCurrent), 0));
  lr_player.stats.attributePoints +=  5;
  gf_ShowStats(lr_player);
  lr_player.stats.attributeText = IntToText(lr_player.stats.attributePoints);
  libNtve_gf_SetDialogItemText(lr_player.stats.attributeDialog.textBox, lr_player.stats.attributeText, lr_player.playerGroupSingle);
  while (lr_player.banks.accessing) {
    Wait(1.0, c_timeGame);
  }
  lr_player.banks.accessing = true;
  BankValueSetFromInt(lr_player.banks.dragonBank, "AP", lr_player.hand, lr_player.stats.attributePoints);
  lr_player.banks.accessing = false;
  UnitSetPropertyFixed(lr_player.dragon, c_unitPropLifePercent, 100.0);
  UnitSetPropertyFixed(lr_player.dragon, c_unitPropEnergyPercent, 100.0);

  lv_dragonEvolution = gf_GetEvolution(lr_player);
  if (UnitLevel(EventUnit()) == (lv_dragonEvolution + 1) * gv_LevelsPerEvolution && lv_dragonEvolution != gv_DragonEvolutionCounts - 1) {
    // Evolve to the next evolution of the type.
    gf_EvolveTo(lr_player, gv_DragonTypes[lr_player.type].unitNames[lv_dragonEvolution+1]);
  }

  gf_StatCheck(lr_player);
  
  return true;
}

void gt_LevelUp_Init () {
  gt_LevelUp = TriggerCreate("gt_LevelUp_Func");
  TriggerAddEventUnitGainLevel(gt_LevelUp, null);
}
